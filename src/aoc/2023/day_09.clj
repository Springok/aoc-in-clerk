;; # ğŸ„ AOC 2023, Day 9: Mirage Maintenance
^{:nextjournal.clerk/visibility :hide-ns}
(ns aoc.2023.day-09
  {:nextjournal.clerk/toc true}
  (:require [nextjournal.clerk :as clerk]
            [aoc.util :as util]
            [clojure.test :refer [deftest is]]))

;; ## After
;;  - `2023-12-09` - è¦ºå¾— Part2 æ‡‰è©²é‚„æ˜¯æœ‰æ›´å¥½çš„è§£æ³•æ‰å°ï¼Œè¦ºå¾—è‡ªå·±ä¸çŸ¥é“å¡åœ¨å“ªè£¡...
;;  - `2023-12-09` - çœ‹äº†åˆ¥äººçš„è§£ç­”ä¹‹å¾Œï¼Œç™¼ç¾æˆ‘æ ¹æœ¬å°±æŠŠ record åéä¾† (reverser) åšå°±å¥½å•¦...ç”¨ä¸åˆ° `left-most`

;; ## Problem
;; çœ‹èµ·ä¾†åˆæ˜¯å€‹ iteration çš„éŠæˆ²å•¦ï¼Œç„¶å¾Œè¦ä¸€å±¤å±¤åæ¨

;; ## Solution
;; ### Part 1

;; ç´€éŒ„ä¸‹æ¯ä¸€æ­¥çš„è®ŠåŒ–å§ï¼Œç„¶å¾Œå† reduce + last + å°±å¯ä»¥è§£æ‰äº†

;; parse é€™é‚Šè¦å°å¿ƒï¼Œå¾Œä¾†ç™¼ç¾è¦è™•ç†è² æ•¸ï¼Œç„¶å¾Œä¹Ÿåœ¨åŠ äº†å€‹ç©ºç™½æ‰é †åˆ©è§£æ‰ï¼Œå¤ªç¬¨äº†ï¼Œå¡äº†ä¸€ä¸‹...
{:nextjournal.clerk/visibility {:result :hide}}
(defn parse-input [file]
  (->> (util/read-file-by-line file)
       (map #(re-seq #".\d+" (str " " %)))
       (map #(map read-string %))))

;; Example
{:nextjournal.clerk/visibility {:result :show}}
(parse-input "../resources/aoc/2023/day9-ex.txt")
{:nextjournal.clerk/visibility {:result :hide}}

;; æ ¸å¿ƒçš„ iteration function, æœƒç”¢ç”Ÿæ‰€æœ‰ history records ç›´åˆ°å…¨éƒ½æ˜¯ 0
(defn process [history]
  (loop [history [history]]
    (let [current-record (last history)
          next-record (->> (partition 2 1 current-record)
                           (map #(apply - (reverse %))))]
      (if (every? zero? current-record)
        (->> history
             (map last)
             (apply +))
        (recur (conj history next-record))))))

(defn part1 [input]
  (->> (parse-input input)
       (map process)
       (reduce +)))

;; ### Part 2
(defn part2 [input]
  (->> (parse-input input)
       (map reverse)
       (map process)
       (reduce +)))

;; ## Answer

;; ### Part 1
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 "../resources/aoc/2023/day9.txt")

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part1 "../resources/aoc/2023/day9.txt")))
;
;; ### Part 2
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 "../resources/aoc/2023/day9.txt")

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part2 "../resources/aoc/2023/day9.txt")))

;; ---
;; ## Example

{:nextjournal.clerk/visibility {:code :show :result :hide}}

(deftest test-example
  (is (= 114 (part1 "../resources/aoc/2023/day9-ex.txt")))
  (is (= 2 (part2 "../resources/aoc/2023/day9-ex.txt"))))

;; ## Obsoleted in first iteration
;; åŸæœ¬çœ‹è¦ºå¾—è »ç°¡å–®çš„ï¼ŒçµæœåŠ ä¸Š parse çš„è™•ç†éŒ¯èª¤ï¼Œç¡¬æ˜¯å¤šå¡äº†æˆ‘ 30 åˆ†é˜ï¼Œå¥½ç„¡è¨€å‘€...
;; å¼„äº†ä¸€å€‹ left-most ä¾†åæ¨æ¯ç­† history å°æ‡‰çš„æ•¸å­—
(defn left-most [history]
  (loop [history history]
    (if (= 2 (count history))
      (apply - history)
      (recur (conj (vec (drop-last 2 history)) (apply - (take-last 2 history)))))))
