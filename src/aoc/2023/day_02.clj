;; # ðŸŽ„ Advent of Code: 2023 Day 2: Cube Conundrum
^{:nextjournal.clerk/visibility :hide-ns}
(ns aoc.2023.day-02
  (:require [nextjournal.clerk :as clerk]
            [aoc.util :as util]
            [clojure.string :as str]
            [clojure.test :refer [deftest is]]))

{:nextjournal.clerk/visibility {:result :hide}}

;; ## Example Data / Input
;; å…‰æ˜¯è³‡æ–™è™•ç†å°±èŠ±äº†æˆ‘å¤§æ¦‚ä¸€å€‹å°æ™‚å‘€.... æˆ‘ç¸½æ„Ÿè¦ºæ‡‰è©²æœ‰æ›´å¥½çš„æ–¹æ³•å¯ä»¥ç›´æŽ¥åˆ†å¥½ data é©åˆå¾Œé¢çš„è™•ç†

(def bag
  {"red" 12 "green" 13, "blue" 14})

(def example-games
  (->> (util/read-file-by-line "../resources/aoc/2023/day2-ex.txt")
       (map #(str/split % #"; "))
       (map (fn [game]
              (->> game
                   (map #(re-seq #"\d+ red|\d+ green|\d+ blue" %)))))))

(def input-games
  (->> (util/read-file-by-line "../resources/aoc/2023/day2.txt")
       (map #(str/split % #"; "))
       (map (fn [game]
              (->> game
                   (map #(re-seq #"\d+ red|\d+ green|\d+ blue" %)))))))

;; `example-games`
{:nextjournal.clerk/visibility {:code :hide :result :show}}
example-games

{:nextjournal.clerk/visibility {:result :hide}}

;; ## (Iteration 1)
;; ### Part 1

;; é€™é‚Šæƒ³èªªç”¨ string ç¡¬å¹¹ + split ä¾†åšåˆ° filter çš„æ•ˆæžœ
{:nextjournal.clerk/visibility {:code :show :result :hide}}
(defn possible? [set bag-set]
  (->> (map #(str/split % #" ") set)
       (every? (fn [[n color]]
                 (<= (read-string n) (get bag-set color))))))

;; Examples
{:nextjournal.clerk/visibility {:code :show :result :show}}
(possible? ["4 blue" "3 red"] bag)
(possible? ["8 green" "6 blue" "20 red"] bag)

{:nextjournal.clerk/visibility {:result :hide}}
;; æ‰€ä»¥ part1 åªæ˜¯ç”¨ `map-indexed` ç„¶å¾Œæ­é…ä¸Šé¢çš„ filter function ç´€éŒ„ä¸‹ game x ç„¶å¾ŒåŠ ç¸½ï¼Œåšåˆ°æˆ‘è¦çš„æ•ˆæžœ
(defn part1 [games]
  (->> games
       (map-indexed (fn [idx game]
                      (if (->> game
                               (every? (fn [set]
                                         (possible? set bag))))
                        (inc idx)
                        0)))
       (apply +)))

;; ### Part 2
;; æœ€å¾Œå±ˆæœäº†å•¦ï¼Œé‚„æ˜¯æŠŠæ¯æ¬¡çš„ game set è½‰æˆé©åˆçš„ map ä¾†è™•ç†ï¼Œå¦‚ä¸‹
(defn set->map [set]
  (->> set
       (map #(str/split % #" "))
       (map (fn[[n color]]
               [color (read-string n)]))
       (into (sorted-map))))

;; Examples
{:nextjournal.clerk/visibility {:code :show :result :show}}
(set->map ["3 blue" "4 red"])

{:nextjournal.clerk/visibility {:result :hide}}
;; `merge-with` çœŸçš„å¤ªè®šäº†å•¦ï¼Œç§’è§£å‹’
;; ç”¨ä¾†è¨ˆç®—æ¯å±€ game çš„ power
(defn power [game]
  (->> game
       (map set->map)
       (apply merge-with max)
       vals
       (apply *)))

;; ç„¶å¾ŒåŠ ç¸½
(defn part2 [games]
  (->> games
       (map power)
       (apply +)))

;; ## Answer

;; ### Part 1
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 input-games)

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part1 input-games)))

;; ### Part 2
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 input-games)

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part2 input-games)))

;; ---
;; ## Example
{:nextjournal.clerk/visibility {:code :hide :result :hide}}

(deftest test-example ;
  (is (= 8 (part1 example-games)))
  (is (= 2286 (part2 example-games))))

{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 example-games)

{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 example-games)
