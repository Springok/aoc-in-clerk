;; # ðŸŽ„ AOC 2023, Day 8: Haunted Wasteland
^{:nextjournal.clerk/visibility :hide-ns}
(ns aoc.2023.day-08
  {:nextjournal.clerk/toc true}
  (:require [nextjournal.clerk :as clerk]
            [aoc.util :as util]
            [clojure.string :as cs]
            [clojure.math.numeric-tower :as math]))

;; ## Problem
;; æ„Ÿè¦ºå°±æ˜¯æ ¹æ“š input ç„¶å¾Œç”¨ iteration ä¾†è·‘åˆ°çµ‚é»ž


;; ## Solution
;; ### Part 1

;; å…ˆè™•ç† data çš„æ ¼å¼

;; é¦–å…ˆæ˜¯æ¯å€‹ map å°æ‡‰çš„å·¦å³

{:nextjournal.clerk/visibility {:result :hide}}
(defn parse-maps [maps]
  (->> maps
      (map #(re-seq #"[A-Z]{3}" %))
      (map (fn [[k & vl]]
             {k vl}))
      (reduce (fn [v r]
                (merge r v)))))

;; å†ä¾†æ˜¯è™•ç† instruction
(defn parse-input [file]
  (let [[instructions _ & maps] (util/read-file-by-line file)]
    [instructions (parse-maps maps)]))

;; Example
{:nextjournal.clerk/visibility {:result :show}}
(parse-input "../resources/aoc/2023/day8-ex.txt")
{:nextjournal.clerk/visibility {:result :hide}}

;; process æ˜¯ç”¨ä¾†æ‰¾åˆ°å°æ‡‰åˆ° ends çš„æ­¥æ•¸ï¼Œå¾žä¸€å€‹ start é–‹å§‹ï¼Œç„¶å¾Œåªè¦åˆ°é”å¦ä¸€å€‹ ends å°±åœä¸‹ä¾†
(defn process [instructions maps start ends]
   (loop [instructions (flatten (repeat (cs/split instructions #"")))
          current-node start
          steps 0]
     (let [direction (first instructions)
           next-nodes (get maps current-node)]
       (if (ends current-node) ;; reach any ends with "Z"
          {:steps steps :end-node current-node}
          (recur (drop 1 instructions) (if (= "R" direction)
                                           (last next-nodes)
                                           (first next-nodes)) (inc steps))))))

(defn part1 [input]
  (let [[instructions maps] (parse-input input)]
    (:steps (process instructions maps "AAA" #{"ZZZ"}))))

;; ### Part 2
;; #### Attempt 1 - åæš´åŠ›è§£ï¼Œå…ˆæ‰¾åˆ°æ‰€æœ‰èµ·é»žè·Ÿçµ‚é»žï¼Œç„¶å¾Œä¸€å€‹å€‹ä¸Ÿé€²åŽ»
;; ä½†æ˜¯è·‘ä¸å®Œï¼Œæ‡‰è©²è¦ç”¨æœ€å°å…¬å€æ•¸ä¹‹é¡žçš„äº†

(defn attempt-1 [instructions maps starts ends]
   (loop [instructions (flatten (repeat (cs/split instructions #"")))
          current-nodes starts
          steps 0]
     (let [direction (first instructions)
           next-nodes (map #(get maps %) current-nodes)]
       (if (= (set current-nodes) ends)
          steps
          (recur (drop 1 instructions) (if (= "R" direction)
                                           (map last next-nodes)
                                           (map first next-nodes)) (inc steps))))))

;; #### Attempt 2 - æœ€å°å…¬å€æ•¸ / æœ€å¤§å…¬å€æ•¸
;; è§£æŽ‰å•¦ï¼Œä¸éŽæœ‰ç”¨åˆ° Math çš„ library å°±æ˜¯äº†

(defn part2 [input]
  (let [[instructions maps] (parse-input input)
        keys' (keys maps)
        starts (set (filter #(cs/ends-with? % "A") keys'))
        ends   (set (filter #(cs/ends-with? % "Z") keys'))
        steps (->> starts
                   (map #(process instructions maps % ends))
                   (map :steps))
        gcd (reduce math/gcd steps)
        divs (map #(/ % gcd) steps)]
     (reduce * (conj divs gcd))))

;; ## Answer

;; ### Part 1
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 "../resources/aoc/2023/day8.txt")
{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part1 "../resources/aoc/2023/day8.txt")))

;; ### Part 2
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 "../resources/aoc/2023/day8.txt")

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part2 "../resources/aoc/2023/day8.txt")))
