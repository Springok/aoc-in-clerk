;; # ðŸŽ„ Advent of Code: 2023 Day 1: Trebuchet?!
^{:nextjournal.clerk/visibility :hide-ns}
(ns aoc.2023.day-01
  (:require
   [nextjournal.clerk :as clerk]
   [aoc.util :as util]
   [clojure.string :as str]
   [clojure.test :refer [deftest is]]))

{:nextjournal.clerk/visibility {:result :hide}}

;; ## Example Data / Input

(def example
  ["1abc2" "pqr3stu8vwx" "a1b2c3d4e5f" "treb7uchet"])

(def example-2
  ["two1nine"
   "eightwothree"
   "abcone2threexyz"
   "xtwone3four"
   "4nineeightseven2"
   "zoneight234"
   "7pqrstsixteen"])

(def input
  (util/read-file-by-line "../resources/aoc/2023/day1.txt"))

;; ## (Iteration 1)

(def mapping
  (let [numbering ["one" "two" "three" "four" "five" "six" "seven" "eight" "nine"]]
    (zipmap
      (flatten [numbering (map str/reverse numbering)])
      (flatten (repeat 2 (range 1 (inc 9)))))))

(def rule-1
  #"\d{1}")

(def rule-2
 #"\d{1}|one|two|three|four|five|six|seven|eight|nine")

(def rule-3
 #"\d{1}|eno|owt|eerht|ruof|evif|xis|neves|thgie|enin")

;; ### Part 1
;; ç¬¬ä¸€é¡Œæ¯”è¼ƒå®¹æ˜“ï¼Œç›´æŽ¥ç”¨ regexp çš„ `d{1}` å°±å¥½äº†ï¼Œæ­é… `re-seq` åŸºæœ¬ä¸Šå°±å¯ä»¥æŠŠæ‰€æœ‰æ•¸å­—æ‰¾åˆ°äº†ï¼Œç„¶å¾Œå–é ­å–å°¾

(defn part1 [records]
  (->> records
       (map #(re-seq rule-1 %))
       (map #(str (first %) (last %)))
       (map read-string)
       (apply +)))

;; ### Part 2
;; ç¬¬äºŒé¡Œå¡äº†ä¸€ä¸‹ï¼Œé›–ç„¶åŠ äº† one åˆ° nine çš„å°ç…§è¡¨ï¼Œä½†æ˜¯å¦‚æžœä¾åºåŽ»è™•ç†çš„è©±ï¼Œæœ‰æ©Ÿæœƒé‡åˆ° edge case åƒæ˜¯ `eighthree` æ‡‰è©²è¦æ˜¯ 83 ä½†æ˜¯å¯èƒ½è®Šæˆ 88
;; é‡æ–°æ€è€ƒä¹‹å¾Œï¼Œå°±æ±ºå®šå¾žé ­æ‰¾åˆ°ç¬¬ä¸€å€‹ç¬¦åˆçš„ï¼Œç„¶å¾Œå¾žå°¾å·´æ‰¾å›žç¬¬ä¸€å€‹ç¬¦åˆçš„ï¼Œç„¶å¾Œåˆèµ·ä¾†æŠŠæ•¸å­—æ‰¾åˆ°
;; ä¸éŽè¦ºå¾—ç¬¬ä¸€å€‹ç‰ˆæœ¬çš„ mapping / regexp (rule 1 / rule 2 / rule 3) æœ‰é»žé†œå°±æ˜¯äº†

(defn convert [word]
  (if ((set (keys mapping)) word)
    (get mapping word)
    word))

(defn find-number [line]
  (->> [(re-find rule-2 line)
        (re-find rule-3 (str/reverse line))]
       (map convert)
       (apply str)
       (read-string)))

(defn part2 [records]
  (->> records
       (map find-number)
       (apply +)))

;; ---
;; ## (Iteration 2)

;; æ­é… lookahead æ˜¯æ¯”è¼ƒç²¾ç°¡çš„è§£æ³•å‘€ï½žä¸éŽä¹Ÿæ˜¯éœ€è¦è½‰æ›æ•¸å­—ï¼Œä½†ä¹Ÿå°±åªè¦è™•ç†æœ€å¾Œä¸€å€‹è·Ÿç¬¬ä¸€å€‹çš„è½‰æ›å°±å¥½å•¦ï½ž

;; ### ->digit
(def digits (->> ["one" "two" "three" "four" "five" "six" "seven" "eight" "nine"]
                 (map-indexed (fn [i w] [w (str (inc i))]))
                 (into {})))

(defn ->digit [word]
  (if ((set (keys digits)) word)
    (get digits word)
    word))

;; ### Solve
(defn solve [records rule]
  (->> records
       (map #(let [tokens (map last (re-seq rule %))
                   first-d (->digit (first tokens))
                   last-d  (->digit (last tokens))]
               (-> (str first-d last-d)
                   read-string)))
       (apply +)))

;; ### Part 1
(defn part1 [records]
  (solve records #"(?=(\d))"))

;; ### Part 2
(defn part2 [records]
  ;; lookahead
  (solve records #"(?=(\d|one|two|three|four|five|six|seven|eight|nine))"))

;; ## Answer

;; ### Part 1
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 input)

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part1 input)))

;; ### Part 2
{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 input)

{:nextjournal.clerk/visibility {:code :hide :result :show}}
(with-out-str
  (time (part2 input)))

;; ---
;; ## Example
{:nextjournal.clerk/visibility {:code :hide :result :hide}}

(deftest test-example ;
  (is (= 142 (part1 example)))
  (is (= 281 (part2 example-2))))

{:nextjournal.clerk/visibility {:code :show :result :show}}
(part1 example)

{:nextjournal.clerk/visibility {:code :show :result :show}}
(part2 example-2)

